<html>
<head>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="keywords" content="">
<meta name="description" content="">
<meta name="author" content="">
<script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r128/three.min.js"></script>
<style>
  @import url('https://fonts.googleapis.com/css2?family=Work+Sans&display=swap');
  body {
      background: #333;
      color: #eee;
      display: grid;
      place-content: center;
      font-family: 'Work Sans', sans-serif;
      margin: 0;
  }
  .sketch-container {
    position: absolute;
    top: 0;
    bottom: 0;
    left: 0;
    right: 0;
    z-index: -1;
  }
  </style>
</head>

<body>
  <h1>fluidity.media</h1>
  <div class="sketch-container" id="sketch-container">
    <div class="sketch-gui" id="sketch-gui"></div>
  </div>

  <script type="x-shader/x-vertex" id="vertexShader">
    // All of this was shamelessly stolen from: https://webgl-shaders.com/mountains-example.html

    #define GLSLIFY 1
    // Common uniforms
    uniform vec2 u_resolution;
    uniform vec2 u_mouse;
    uniform float u_time;
    uniform float u_frame;
    
    // Common varyings
    varying vec3 v_position;
    varying vec3 v_normal;
    
    /*
     * GLSL textureless classic 2D noise "cnoise",
     * with an RSL-style periodic variant "pnoise".
     * Author:  Stefan Gustavson (stefan.gustavson@liu.se)
     * Version: 2011-08-22
     *
     * Many thanks to Ian McEwan of Ashima Arts for the
     * ideas for permutation and gradient selection.
     *
     * Copyright (c) 2011 Stefan Gustavson. All rights reserved.
     * Distributed under the MIT license. See LICENSE file.
     * https://github.com/stegu/webgl-noise
     */
    
    vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    
    vec4 permute(vec4 x) {
        return mod289(((x * 34.0) + 1.0) * x);
    }
    
    vec4 taylorInvSqrt(vec4 r) {
        return 1.79284291400159 - 0.85373472095314 * r;
    }
    
    vec2 fade(vec2 t) {
        return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
    }
    
    float cnoise(vec2 P) {
        vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
        vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
        Pi = mod289(Pi);
        vec4 ix = Pi.xzxz;
        vec4 iy = Pi.yyww;
        vec4 fx = Pf.xzxz;
        vec4 fy = Pf.yyww;
    
        vec4 i = permute(permute(ix) + iy);
    
        vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0;
        vec4 gy = abs(gx) - 0.5;
        vec4 tx = floor(gx + 0.5);
        gx = gx - tx;
    
        vec2 g00 = vec2(gx.x, gy.x);
        vec2 g10 = vec2(gx.y, gy.y);
        vec2 g01 = vec2(gx.z, gy.z);
        vec2 g11 = vec2(gx.w, gy.w);
    
        vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
        g00 *= norm.x;
        g01 *= norm.y;
        g10 *= norm.z;
        g11 *= norm.w;
    
        float n00 = dot(g00, vec2(fx.x, fy.x));
        float n10 = dot(g10, vec2(fx.y, fy.y));
        float n01 = dot(g01, vec2(fx.z, fy.z));
        float n11 = dot(g11, vec2(fx.w, fy.w));
    
        vec2 fade_xy = fade(Pf.xy);
        vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
        float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
        return 2.3 * n_xy;
    }
    
    // The plane tile size, the flying speed and the maximum mountain height
    uniform float u_tileSize;
    uniform float u_speed;
    uniform float u_maxHeight;
    
    // Varying containing the terrain elevation
    varying float v_elevation;
    
    // Calculates the vertex displaced position
    vec3 getDisplacedPosition(vec3 position) {
      // Calculate the total flying distance
      float distance = u_speed * u_time;
    
      // Calculate the vertex horizontal shift
      float h_shift = mod(distance, u_tileSize);
    
      // Calculate the vertex vertical shift
      float v_shift = u_maxHeight * cnoise(0.4 * floor(vec2(position.x, position.z - distance) / u_tileSize));
    
      // Flatten the bottom to simulate the lakes
      v_shift = max(-0.8 * u_maxHeight, v_shift);
    
      return position + vec3(0.0, v_shift, h_shift);
    }
    
    /*
     * The main program
     */
    void main() {
      // Calculate the new vertex position
      vec3 new_position = getDisplacedPosition(position);
    
      // Calculate the modelview position
      vec4 mv_position = modelViewMatrix * vec4(new_position, 1.0);
    
      // Save the varyings
      v_position = mv_position.xyz;
      v_elevation = 0.5 * (u_maxHeight + new_position.y);
    
      // Vertex shader output
      gl_Position = projectionMatrix * mv_position;
    }
    
        </script>
        <script type="x-shader/x-fragment" id="fragmentShader">
          #define GLSLIFY 1
          // Common uniforms
          uniform vec2 u_resolution;
          uniform vec2 u_mouse;
          uniform float u_time;
          uniform float u_frame;
    
          // Common varyings
          varying vec3 v_position;
          varying vec3 v_normal;
    
          /*
          *  Calculates the normal vector at the given position
          *
          *  Uses this fix for some mobiles:
          *  https://stackoverflow.com/questions/20272272/standard-derivatives-from-fragment-shader-dfdx-dfdy-dont-run-correctly-in-a
          */
          vec3 calculateNormal(vec3 position) {
              vec3 fdx = vec3(dFdx(position.x), dFdx(position.y), dFdx(position.z));
              vec3 fdy = vec3(dFdy(position.x), dFdy(position.y), dFdy(position.z));
              vec3 normal = normalize(cross(fdx, fdy));
    
              if (!gl_FrontFacing) {
                  normal = -normal;
              }
    
              return normal;
          }
    
          /*
          *  Calculates the diffuse factor produced by the light illumination
          */
          float diffuseFactor(vec3 normal, vec3 light_direction) {
              float df = dot(normalize(normal), normalize(light_direction));
    
              if (gl_FrontFacing) {
                  df = -df;
              }
    
              return max(0.0, df);
          }
    
          // The plane tile size, the maximum mountain height and the sky color
          uniform float u_tileSize;
          uniform float u_maxHeight;
          uniform vec3 u_skyColor;
    
          // Varying containing the terrain elevation
          varying float v_elevation;
    
          /*
          * The main program
          */
          void main() {
            // Calculate the new normal vector
            vec3 new_normal = calculateNormal(v_position);
    
            // Use the mouse position to define the light direction
            float min_resolution = min(u_resolution.x, u_resolution.y);
            vec3 light_direction = -vec3((u_mouse - 0.5 * u_resolution) / min_resolution, 0.25);
    
            // Set the default surface color
            vec3 surface_color = vec3(0.2, 0.2, 0.2);
    
            // Apply the light diffusion factor
            surface_color *= diffuseFactor(new_normal, light_direction);
    
            // Add a fog effect
            float fog_factor = clamp(0.0, 1.0, -(0.9) * v_position.z / (15.0 * u_tileSize));
            surface_color = mix(surface_color, u_skyColor, fog_factor);
    
            // Fragment shader output
            gl_FragColor = vec4(surface_color, 1.0);
          }
    
        </script>
    
        <script>
        window.onload = function() {
          runSketch();
        };
    
          function runSketch() {
            var renderer, scene, camera, clock, stats, skyColor, uniforms;
    
            init();
            animate();
    
            /*
            * Initializes the sketch
            */
            function init() {
              // Initialize the WebGL renderer
              renderer = new THREE.WebGLRenderer({
                antialias : true
              });
              renderer.setPixelRatio(window.devicePixelRatio);
              renderer.setSize(window.innerWidth, window.innerHeight);
    
              // Add the renderer to the sketch container
              var container = document.getElementById("sketch-container");
              container.appendChild(renderer.domElement);
    
              // Initialize the scene
              scene = new THREE.Scene();
    
              // Initialize the camera
              camera = new THREE.PerspectiveCamera(75, window.innerWidth / window.innerHeight, 0.1, 5000);
              camera.position.z = 30;
    
              // Initialize the clock
              clock = new THREE.Clock(true);
    
              // Set the sky color
              skyColor = new THREE.Color(0.1, 0.1, 0.1);
              renderer.setClearColor(skyColor);
    
              // Create the plane geometry
              var planeSize = 320;
              var planeSegments = 32;
              var geometry = new THREE.PlaneGeometry(planeSize, planeSize, planeSegments, planeSegments);
              geometry.rotateX(-Math.PI / 2);
    
              // Define the shader uniforms
              uniforms = {
                u_time : {
                  type : "f",
                  value : 0.0
                },
                u_frame : {
                  type : "f",
                  value : 0.0
                },
                u_resolution : {
                  type : "v2",
                  value : new THREE.Vector2(window.innerWidth, window.innerHeight)
                      .multiplyScalar(window.devicePixelRatio)
                },
                u_mouse : {
                  type : "v2",
                  value : new THREE.Vector2(0.7 * window.innerWidth, window.innerHeight)
                      .multiplyScalar(window.devicePixelRatio)
                },
                u_tileSize : {
                  type : "f",
                  value : planeSize / planeSegments
                },
                u_speed : {
                  type : "f",
                  value : 30.0
                },
                u_maxHeight : {
                  type : "f",
                  value : 20.0
                },
                u_skyColor : {
                  type : "v3",
                  value : skyColor
                }
              };
    
              // Create the shader material
              var material = new THREE.ShaderMaterial({
                uniforms : uniforms,
                vertexShader : document.getElementById("vertexShader").textContent,
                fragmentShader : document.getElementById("fragmentShader").textContent,
                side : THREE.FrontSide,
                transparent : true,
                extensions : {
                  derivatives : true
                }
              });
    
              // Create the mesh and add it to the scene
              var mesh = new THREE.Mesh(geometry, material);
              mesh.rotateX(Math.PI / 9);
              scene.add(mesh);
    
              // Add the event listeners
              window.addEventListener("resize", onWindowResize, false);
            }
    
            /*
            * Animates the sketch
            */
            function animate() {
              requestAnimationFrame(animate);
              render();
              stats.update();
            }
    
            /*
            * Renders the sketch
            */
            function render() {
              uniforms.u_time.value = clock.getElapsedTime();
              uniforms.u_frame.value += 1.0;
              renderer.render(scene, camera);
            }
    
            /*
            * Updates the renderer size, the camera aspect ratio and the uniforms when the window is resized
            */
            function onWindowResize(event) {
              // Update the renderer
              renderer.setSize(window.innerWidth, window.innerHeight);
    
              // Update the camera
              camera.aspect = window.innerWidth / window.innerHeight;
              camera.updateProjectionMatrix();
    
              // Update the resolution uniform
              uniforms.u_resolution.value.set(window.innerWidth, window.innerHeight).multiplyScalar(window.devicePixelRatio);
            }
          }
        </script>

</body>
</html>